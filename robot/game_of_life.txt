*** Settings ***
Library           Selenium2Library
Suite Teardown    Close All Browsers

*** variables ***
${HOST}           http://127.0.0.1:3333
${PAGE}           /index.html
${BROWSER}        gc

*** Test Cases ***
Basic
    When Open Page
    Then Page Should Contain    Game of Life with Javascript and AngularJS
    And Page Should Contain Universe With 25 x 40 Dimension
    When Click To Set Live Cell Boat Pattern
    Then Universe Should Display Boat Pattern
    When Click Reset Button
    Then Universe Should Not Display Boat Pattern

Evolve
    Given Go To    ${HOST}
    When Click To Set Live Cell Beacon Pattern
    Then Universe Should Display Beacon Pattern
    When Click Step Evolve
    Then Universe Should Display Beacon Pattern period 2
    When Click Step Evolve
    Then Universe Should Display Beacon Pattern

Auto Evolve
    [Documentation]    Click Evolve will set interval for evolve and click again will reset interval
    Given Go To    ${HOST}
    When Click To Set Live Cell Beacon Pattern
    Then Universe Should Display Beacon Pattern
    When Click Evolve
    Then Step Evolve Button Should Be Disabled
    Then Reset Button Should Be Disabled
    And Universe Should Display Beacon Pattern period 2
    And Universe Should Display Beacon Pattern
    And Universe Should Display Beacon Pattern period 2
    When Click Evolve
    Then Step Evolve Button Should Be Enabled

*** Keywords ***
Page Should Contain Universe With ${height} x ${width} Dimension
    Page Should Contain Element     cell-${height}-${width}

Click To Set Live Cell Boat Pattern
    Click Element    cell-1-1
    Click Element    cell-1-2
    Click Element    cell-2-1
    Click Element    cell-2-3
    Click Element    cell-3-2

Universe Should Display Boat Pattern
    Page Should Contain Element    css=#cell-1-1.active
    Page Should Contain Element    css=#cell-1-2.active
    Page Should Contain Element    css=#cell-2-1.active
    Page Should Contain Element    css=#cell-2-3.active
    Page Should Contain Element    css=#cell-3-2.active

Click Reset Button
    Click Button    reset

Universe Should Not Display Boat Pattern
    Page Should Not Contain Element    css=#cell-1-1.active
    Page Should Not Contain Element    css=#cell-1-2.active
    Page Should Not Contain Element    css=#cell-2-1.active
    Page Should Not Contain Element    css=#cell-2-3.active
    Page Should Not Contain Element    css=#cell-3-2.active

Click To Set Live Cell Beacon Pattern
    Click Element    cell-1-1
    Click Element    cell-1-2
    Click Element    cell-2-1
    Click Element    cell-2-2
    Click Element    cell-3-3
    Click Element    cell-3-4
    Click Element    cell-4-3
    Click Element    cell-4-4

Click Step Evolve
    Click Button    step-evolve

Universe Should Display Beacon Pattern
    Wait    Page Should Contain Element    css=#cell-1-1.active
    Wait    Page Should Contain Element    css=#cell-1-2.active
    Wait    Page Should Contain Element    css=#cell-2-1.active
    Wait    Page Should Contain Element    css=#cell-2-2.active
    Wait    Page Should Contain Element    css=#cell-3-3.active
    Wait    Page Should Contain Element    css=#cell-3-4.active
    Wait    Page Should Contain Element    css=#cell-4-3.active
    Wait    Page Should Contain Element    css=#cell-4-4.active

Universe Should Display Beacon Pattern period 2
    Wait    Page Should Contain Element    css=#cell-1-1.active
    Wait    Page Should Contain Element    css=#cell-1-2.active
    Wait    Page Should Contain Element    css=#cell-2-1.active
    Wait    Page Should Not Contain Element    css=#cell-2-2.active
    Wait    Page Should Not Contain Element    css=#cell-3-3.active
    Wait    Page Should Contain Element    css=#cell-3-4.active
    Wait    Page Should Contain Element    css=#cell-4-3.active
    Wait    Page Should Contain Element    css=#cell-4-4.active

Click Evolve
    Click Button    evolve

Step Evolve Button Should Be Disabled
    Element Should Be Disabled    step-evolve

Step Evolve Button Should Be Enabled
    Element Should Be Enabled     step-evolve

Reset Button Should Be Disabled
    Element Should Be Disabled    reset

Open Page
    Open Browser    ${HOST}${PAGE}   browser=${BROWSER}
    Maximize Browser Window

Wait
    [Arguments]    @{args}
    Wait Until Keyword Succeeds    2s    0.2s     @{args}